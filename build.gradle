plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
	id 'java'
	id 'checkstyle'
	id 'pmd'
	id 'jacoco'
	id 'org.owasp.dependencycheck' version '7.1.0.1'
 }

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

checkstyle {
	maxWarnings = 0
	toolVersion = '9.2.1'
	getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
	consoleOutput = true
	toolVersion = "6.45.0"
	ruleSets = []
	ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
	dependsOn test // tests are required to run before generating the report
}

dependencyCheck {
	failBuildOnCVSS = 0
	suppressionFile = 'config/owasp/suppressions.xml'
	skipConfigurations = [
		"checkstyle",
		"compileOnly",
		"pmd"
	]
}

check.dependsOn dependencyCheckAnalyze

bootJar {
	archiveFileName = "spring-boot-template.jar"

	manifest {
		attributes('Implementation-Version': project.version.toString())
	}
}
